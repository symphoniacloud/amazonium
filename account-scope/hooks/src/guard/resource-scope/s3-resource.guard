# S3 Resource-Level Guard

let s3_buckets = Resources.*[ Type == 'AWS::S3::Bucket']

rule S3_BUCKET_ENCRYPTION_KMS when %s3_buckets !empty {
  %s3_buckets.Properties.BucketEncryption.ServerSideEncryptionConfiguration !empty
  %s3_buckets.Properties.BucketEncryption.ServerSideEncryptionConfiguration[*].ServerSideEncryptionByDefault.SSEAlgorithm == "aws:kms"
  %s3_buckets.Properties.BucketEncryption.ServerSideEncryptionConfiguration[*].ServerSideEncryptionByDefault.KMSMasterKeyID exists
  <<
    Violation: S3 buckets must use aws:kms encryption with a KMSMasterKeyID specified
  >>
}

rule S3_BUCKET_NAMING_PATTERN when %s3_buckets !empty {
  %s3_buckets.Properties.BucketName exists

  let arn_pattern = "^arn:aws:cloudformation:([^:]+):([^:]+):stack/([^/]+)/.*"
  let expected_stack = to_lower(regex_replace(HookContext.StackId, %arn_pattern, "$3"))
  let expected_account = regex_replace(HookContext.StackId, %arn_pattern, "$2")
  let expected_region = regex_replace(HookContext.StackId, %arn_pattern, "$1")

  let name = %s3_buckets.Properties.BucketName
  let bucket_name_pattern = "^([^-]+(?:-[^-]+)*?)-([0-9]{12})-([a-z0-9-]+)-([a-z0-9]+)$"
  let bucket_stack = regex_replace(%name, %bucket_name_pattern, "$1")
  let bucket_account = regex_replace(%name, %bucket_name_pattern, "$2")
  let bucket_region = regex_replace(%name, %bucket_name_pattern, "$3")
  let bucket_suffix = regex_replace(%name, %bucket_name_pattern, "$4")

  %bucket_stack == %expected_stack
  %bucket_account == %expected_account
  %bucket_region == %expected_region
  %bucket_suffix != ""
  %name == to_lower(%name)
  <<
    Violation: Bucket name must be {stackName}-{account}-{region}-{suffix} (lowercase alphanumeric)
  >>
}

rule S3_BUCKET_NO_WEBSITE_HOSTING when %s3_buckets !empty {
  %s3_buckets.Properties.WebsiteConfiguration !exists
  <<
    Violation: S3 buckets must not have WebsiteConfiguration enabled
  >>
}

rule S3_BUCKET_NO_CORS when %s3_buckets !empty {
  %s3_buckets.Properties.CorsConfiguration !exists
  <<
    Violation: S3 buckets must not have CorsConfiguration enabled
  >>
}

rule S3_BUCKET_OWNERSHIP_CONTROLS when %s3_buckets !empty {
  %s3_buckets.Properties.OwnershipControls.Rules !empty
  %s3_buckets.Properties.OwnershipControls.Rules[*].ObjectOwnership == "BucketOwnerEnforced"
  <<
    Violation: S3 buckets must have OwnershipControls with ObjectOwnership set to BucketOwnerEnforced
  >>
}

rule S3_BUCKET_ACCESS_CONTROL when %s3_buckets !empty {
  %s3_buckets.Properties.AccessControl == "BucketOwnerFullControl"
  <<
    Violation: S3 buckets must have AccessControl set to BucketOwnerFullControl
  >>
}

rule S3_BUCKET_PUBLIC_ACCESS_BLOCKED when %s3_buckets !empty {
  %s3_buckets.Properties.PublicAccessBlockConfiguration.BlockPublicAcls == true
  %s3_buckets.Properties.PublicAccessBlockConfiguration.BlockPublicPolicy == true
  %s3_buckets.Properties.PublicAccessBlockConfiguration.IgnorePublicAcls == true
  %s3_buckets.Properties.PublicAccessBlockConfiguration.RestrictPublicBuckets == true
  <<
    Violation: S3 buckets must have all PublicAccessBlockConfiguration settings set to true
  >>
}

rule S3_BUCKET_LIFECYCLE_MULTIPART_EXPIRY when %s3_buckets !empty {
  %s3_buckets.Properties.LifecycleConfiguration.Rules !empty
  %s3_buckets.Properties.LifecycleConfiguration.Rules[*].AbortIncompleteMultipartUpload.DaysAfterInitiation <= 7
  <<
    Violation: S3 buckets must have AbortIncompleteMultipartUpload with DaysAfterInitiation <= 7 on all lifecycle rules
  >>
}

rule S3_BUCKET_REQUIRED_TAGS when %s3_buckets !empty {
  %s3_buckets.Properties.Tags exists

  let app_id_tags = %s3_buckets.Properties.Tags[Key == "ApplicationId"]
  %app_id_tags !empty
  %app_id_tags[*].Value != ""

  let catalogue_tags = %s3_buckets.Properties.Tags[Key == "dCatalogue"]
  %catalogue_tags !empty
  %catalogue_tags[*].Value != ""

  let business_unit_tags = %s3_buckets.Properties.Tags[Key == "BusinessUnit"]
  %business_unit_tags !empty
  %business_unit_tags[*].Value != ""
  <<
    Violation: S3 buckets must have non-empty ApplicationId, dCatalogue, and BusinessUnit tags
  >>
}
