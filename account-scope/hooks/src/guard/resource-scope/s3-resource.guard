# S3 Resource-Level Guard

let s3_buckets = Resources.*[ Type == 'AWS::S3::Bucket']

rule S3_BUCKET_ENCRYPTION_ALGORITHM when %s3_buckets !empty {
  %s3_buckets.Properties.BucketEncryption exists
  %s3_buckets.Properties.BucketEncryption.ServerSideEncryptionConfiguration exists
  %s3_buckets.Properties.BucketEncryption.ServerSideEncryptionConfiguration[*].ServerSideEncryptionByDefault exists
  %s3_buckets.Properties.BucketEncryption.ServerSideEncryptionConfiguration[*].ServerSideEncryptionByDefault.SSEAlgorithm in ["aws:kms","AES256"]
  <<
    Violation: S3 buckets must use KMS or AES256 encryption
  >>
}

rule S3_BUCKET_NAMING_PATTERN when %s3_buckets !empty {
  %s3_buckets.Properties.BucketName exists

  let arn_pattern = "^arn:aws:cloudformation:([^:]+):([^:]+):stack/([^/]+)/.*"
  let expected_stack = to_lower(regex_replace(HookContext.StackId, %arn_pattern, "$3"))
  let expected_account = regex_replace(HookContext.StackId, %arn_pattern, "$2")
  let expected_region = regex_replace(HookContext.StackId, %arn_pattern, "$1")

  let name = %s3_buckets.Properties.BucketName
  let bucket_name_pattern = "^([^-]+(?:-[^-]+)*?)-([0-9]{12})-([a-z0-9-]+)-([a-z0-9]+)$"
  let bucket_stack = regex_replace(%name, %bucket_name_pattern, "$1")
  let bucket_account = regex_replace(%name, %bucket_name_pattern, "$2")
  let bucket_region = regex_replace(%name, %bucket_name_pattern, "$3")
  let bucket_suffix = regex_replace(%name, %bucket_name_pattern, "$4")

  %bucket_stack == %expected_stack
  %bucket_account == %expected_account
  %bucket_region == %expected_region
  %bucket_suffix != ""
  %name == to_lower(%name)
  <<
    Violation: Bucket name must be {stackName}-{account}-{region}-{suffix} (lowercase alphanumeric)
  >>
}