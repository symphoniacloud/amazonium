# S3 Resource-Level Guard

let s3_buckets = Resources.*[ Type == 'AWS::S3::Bucket']

rule S3_BUCKET_ENCRYPTION_ALGORITHM when %s3_buckets !empty {
  %s3_buckets.Properties.BucketEncryption exists
  %s3_buckets.Properties.BucketEncryption.ServerSideEncryptionConfiguration exists
  %s3_buckets.Properties.BucketEncryption.ServerSideEncryptionConfiguration[*].ServerSideEncryptionByDefault exists
  %s3_buckets.Properties.BucketEncryption.ServerSideEncryptionConfiguration[*].ServerSideEncryptionByDefault.SSEAlgorithm in ["aws:kms","AES256"]
  <<
    Violation: S3 bucket encryption must be set and use KMS or AES256 algorithm
    Fix: Set ServerSideEncryptionByDefault.SSEAlgorithm to either "aws:kms" or "AES256"
  >>
}

rule S3_BUCKET_NAMING_PATTERN when %s3_buckets !empty {
  # Bucket name must be set and follow pattern: {stackName}-{account}-{region}-bucket

  %s3_buckets.Properties.BucketName exists

  # FROM HookContext.StackId
  # CAPTURE region, then account ID, then stack-name
  # OUTPUT expected bucket name - {stack-name}-{account}-{region}-bucket
  let pattern_step1 = regex_replace(HookContext.StackId, "^arn:aws:cloudformation:([^:]+):([^:]+):stack/([^/]+)/.*", "$3-$2-$1-bucket")
  let expected_pattern = to_lower(%pattern_step1)

  %s3_buckets.Properties.BucketName == %expected_pattern
  <<
    Violation: S3 bucket name must match the pattern {stackName}-{account}-{region}-bucket using runtime context values
    Fix: Bucket name must be exactly: %expected_pattern (derived from stack: %stack_name, account: %account, region: %region)
  >>
}